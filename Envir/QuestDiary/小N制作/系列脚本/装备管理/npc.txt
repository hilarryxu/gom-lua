[@装备管理_入口]
{
;; BEGIN @装备管理_入口


#IF
#ACT
  SetUpgradeItem 0

#IF
  CheckBoxItemCount 0 1
#ACT
  ReturnBoxItem 0

#IF
#ACT
  MOV N20 0
  OpenMerchantBigDlg 21 130 0 0 1 1 0
  GOTO @FORM_装备管理

[@FORM_装备管理]
#IF
#ACT
  Mov N70 99
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua build_form S10 <　　>
#SAY
<Img:113:0:892:5/@exit>\ \
<　　　><装备管理/AUTOCOLOR=154>\
<　　　><工作人员/FCOLOR=19> <尊敬的玩家您好/FCOLOR=151>{【<$SERVERNAME>感谢一路有您!】/FCOLOR=239}\
<　　　><------------------------------------------------------------------------------------------------------------------/AUTOCOLOR=90>\
<$STR(S10)> \


; 确认记录(位置)
[@LBL_确认记录]
#IF
#ACT
  SetUpgradeItem 0

#IF
  CheckBoxItemCount 0 1
#ACT
  ; add_record(位置, 装备Idx, 装备名称)
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua add_record <$STR(S$NN_NPCPARAM_1)> <$BOXITEM[0].MAKEINDEX> <$BOXITEM[0].NAME>
  ReturnBoxItem 0
  GOTO @FORM_装备管理
  BREAK
#ELSEACT
  GOTO @FORM_装备管理
  MESSAGEBOX 请先放入要记录的装备
  BREAK


; 清除记录(位置)
[@LBL_清除记录]
#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua del_record <$STR(S$NN_NPCPARAM_1)>
  GOTO @FORM_装备管理
  BREAK


[@LBL_一键记录]
#IF
#ACT
  MESSAGEBOX 您确定要一键记录吗（会覆盖已有记录）? @LBL_确定一键记录 @UNK_取消


[@LBL_确定一键记录]
#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua record_all
  GOTO @FORM_装备管理
  MESSAGEBOX 一键记录完毕！
  BREAK


[@LBL_清空记录]
#IF
#ACT
  MESSAGEBOX 您确定要清空记录吗? @LBL_确定清空记录 @UNK_取消


[@LBL_确定清空记录]
#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua clear_all
  GOTO @FORM_装备管理
  MESSAGEBOX 清空记录完毕!
  BREAK


; 查此物品(位置, 装备Idx)
[@LBL_查此物品]
#IF
#ACT
  MOV N20 <$STR(S$NN_NPCPARAM_2)>
  GOTO @LBL_查询
  BREAK


[@LBL_查询]
; 检查待查询装备Idx数值
#IF
  NOT LARGE N20 0
#ACT
  MOV N20 0
  GOTO @FORM_装备管理
  MESSAGEBOX 请先输入您要查询的装备Idx序列号
  BREAK

#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua search_record <$STR(N20)> N98 S98

#IF
  NOT EQUAL N98 0
#ACT
  MOV N20 0
  GOTO @FORM_装备管理
  MESSAGEBOX <$STR(S98)>
  BREAK
#ELSEACT
  MOV N20 0
  SENDMSG 6 <$STR(S98)>
  GOTO @FORM_装备管理
  BREAK


[@InPutInteger20]
#OR
  SMALL N20 1
  LARGE N20 2000000000
#ACT
  MOV N20 0
  GOTO @FORM_装备管理
  MESSAGEBOX 请输入大于0且小于20亿的装备Idx序列号
  BREAK
#ELSEACT
  GOTO @FORM_装备管理
  BREAK


[@ItemIntoBox0]
#IF
#ACT
  SetUpgradeItem 0

#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua check_allow_item <$BOXITEM[0].NAME> N98

#IF
  EQUAL N98 0
#ACT
  Mov N20 <$BOXITEM[0].MAKEINDEX>
  GOTO @FORM_装备管理
  BREAK
#ELSEACT
  UnallowItemIntoBox
  GOTO @FORM_装备管理
  MESSAGEBOX [提示]：很抱歉，该物品不允许进行装备Idx记录，\　\请查看[允许记录列表]
  BREAK


[@ItemOutBox0]
#IF
#ACT
  Mov N20 0
  UpDateBoxItem 0
  GOTO @FORM_装备管理
  BREAK


;; END @装备管理_入口
}


[@装备管理_捡取触发]
{

#IF
#ACT
  NnDoLuaScript 小N制作/系列脚本/装备管理/script.lua on_pickup_item <$CURRTEMMAKEINDEX>

}
